// XLL file event followed by Excel.Application COM object making network connections
// Could indicate the presense of XLL dropper malware
// read more at https://github.com/optiv/Dent#remote-xll-payload-mode
let timeframe = 2h;
let lookupWindow = 1min;
let lookupBin = lookupWindow / 2.0;
DeviceFileEvents
| where Timestamp > ago(timeframe)
| where FileName endswith ".xll"
| where FolderPath !endswith "\\Library\\Analysis\\ANALYS32.XLL"
| project DeviceId, DeviceName, FileName, FolderPath, Start=Timestamp, ReportId, TimeKey=bin(Timestamp, lookupBin)
| join kind=inner (
    DeviceNetworkEvents
    | where Timestamp > ago(timeframe)
    | where InitiatingProcessFileName =~ "excel.exe"
    | where InitiatingProcessParentFileName =~ "svchost.exe"
    | project DeviceName, RemoteUrl, RemotePort, ActionType,
        Username = InitiatingProcessAccountName,
        Email = InitiatingProcessAccountUpn,
        End = Timestamp,
        TimeKey = range(bin(Timestamp-lookupWindow, lookupBin),
                        bin(Timestamp, lookupBin),
                        lookupBin)
    | mv-expand TimeKey to typeof(datetime)
    ) on DeviceName, TimeKey
| where (End - Start) between (0min .. lookupWindow)
| extend Timestamp=Start
